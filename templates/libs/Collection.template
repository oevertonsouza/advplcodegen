#include "TOTVS.CH"
#INCLUDE 'FWMVCDEF.CH'

#DEFINE COLLECTION 2

#DEFINE NIVEL 1
#DEFINE SUBNIVEL 2

#DEFINE RELFIELD 1
#DEFINE FIELD 2

#DEFINE N_TO_N 1    // Rela��o N para N
#DEFINE ONE_TO_N 2  // Rela��o 1 para N
#DEFINE N_TO_ONE 3  // Rela��o N para 1
#DEFINE ZERO_TO 4   // Rela��o 0 para 1 ou N
#DEFINE TO_ZERO 5   // Rela��o N ou 1 para 0

#DEFINE NONE 0    // Behavior Atribui altera��es somente a cola��o principal
#DEFINE CASCADE 1 // Behavior Realiza todas as opera��es em cascata

/*/{Protheus.doc}
    Collection
    Classe abstrata de uma Cole��o de registros de uma entifade.
    Esta classe deve orquestrar as opera��es feitas em cole��es de registros.
    Isso envolve:
        - Buscas de registros espec�ficos
        - Buscas de cole��es de registros
        - Itera��es sobre cada registro retornado
        - Dele��o ou atualiza��o de cole��es de registros

    @type  Class
    @author everton.mateus
    @since 20190222
/*/

Class ${prefix}Collection
	   
    Data oDao
    Data oMapper
    Data hMap
    Data lFound
    Data nRecno
    Data cError
    Data lFault
    Data hChildren    

    Method New() Constructor
    Method destroy()
    Method found()
    Method setError(cMsg)
    Method getError()
    Method hasNext()
    Method getNext()
    Method mapFromJson(cJson)
    Method mapFromDao()
    Method mapDaoJson()    
    Method goTop()
    Method applyPageSize(cPage,cPageSize)
    Method getPageSize()
    Method getDao()
    Method getQuery()
    Method applyOrder(cOrder)
    Method setValue(cProperty,xData)
    Method getValue(cProperty)
    Method commit()
    Method insert()
    Method update()
    Method delete()
    Method buscar()
    Method bscChaPrim()
    Method setEntity(oEntity)
    Method setRelation(oRelation)
    Method getRelation(cName)
    Method setKeyRelation()
    Method listRelations()
    Method delRelation()
    Method sethMap(hMap)
    Method isRelation(cName)    
    Method applyExpand(aExpand)

EndClass

Method New() Class ${prefix}Collection
    self:lFound := .F.
    self:lFault := .F.
    self:cError := ""
    self:hMap := THashMap():New()
    self:hChildren := THashMap():New()    
return self

Method destroy() Class ${prefix}Collection

    if !empty(self:getDao())
        self:getDao():destroy()
        FreeObj(self:oDao)
        self:oDao := nil
    endif

    if !empty(self:oMapper)
        FreeObj(self:oMapper)
        self:oMapper := nil
    endif
    
    if !empty(self:hMap)
        self:hMap:clean()
        FreeObj(self:hMap)
        self:hMap := nil
    endif

return

Method sethMap(hMap) Class ${prefix}Collection
    self:hMap := hMap
    self:oDao:setHMap(hMap)
return

Method found() Class ${prefix}Collection
return self:lFound

Method setError(cMsg) Class ${prefix}Collection
    self:cError := cMsg
    self:lFault := .T.
return

Method getError() Class ${prefix}Collection
return self:cError

Method hasNext() Class ${prefix}Collection
return self:getDao():hasNext(self:nRecno)

Method getNext() Class ${prefix}Collection
    self:oMapper:setEntity(self:initEntity())
    self:oMapper:mapFromDao(self:getDao())
    self:hMap := self:oMapper:getEntity():getHMap()
    self:setKeyRelation()
    self:nRecno++
return self:oMapper:getEntity()

Method mapFromJson(oJson) Class ${prefix}Collection
    self:oMapper:setEntity(self)
    self:oMapper:mapFromJson(oJson)
return self:oMapper:getEntity()

Method mapFromDao() Class ${prefix}Collection
    self:oMapper:setEntity(self)
    self:oMapper:mapFromDao(self:getDao())
return self:oMapper:getEntity()

Method mapDaoJson(oJson) Class ${prefix}Collection
    self:mapFromDao()
    self:mapFromJson(oJson)
return self:oMapper:getEntity()

Method goTop() Class ${prefix}Collection
    self:nRecno := 1
    self:getDao():posReg(self:nRecno)
return

Method applyPageSize(cPage,cPageSize) Class ${prefix}Collection
    self:getDao():setNumPage(cPage)
    self:getDao():setPageSize(cPageSize)
return

Method getPageSize() Class ${prefix}Collection
return self:getDao():getPageSize()

Method getDao() Class ${prefix}Collection
Return self:oDao

Method getQuery() Class ${prefix}Collection
Return self:getDao():getQuery()

Method applyOrder(cOrder) Class ${prefix}Collection
    self:getDao():setOrder(cOrder)
return

Method setValue(cProperty,xData) Class ${prefix}Collection
    self:getDao():setValue(cProperty,xData)
Return self:hMap:set(cProperty,xData)

Method getValue(cProperty) Class ${prefix}Collection
	Local anyValue := ""
	self:hMap:get(cProperty,@anyValue)
Return anyValue

Method commit() Class ${prefix}Collection
    self:lFound := self:getDao():commit()
    If !self:found()
        self:setError("N�o conseguiu incluir o registro. " + self:getDao():getError() )
    EndIf
Return self:found()

Method insert() Class ${prefix}Collection
    self:lFound := self:getDao():insert()
    If !self:found()
        self:setError("N�o conseguiu incluir o registro. " + self:getDao():getError() )
    EndIf
Return self:found()

Method update() Class ${prefix}Collection
Return self:commit()

Method delete() Class ${prefix}Collection
    self:lFound := self:getDao():delete()
    If !self:found()
        self:setError("N�o conseguiu deletar o registro. " + self:getDao():getError())
    EndIf
Return self:found()

Method delRelation() Class ${prefix}Collection
    self:lFound := self:getDao():delRelation()
    If !self:found()
        self:setError("N�o conseguiu deletar o registro. " + self:getDao():getError())
    EndIf
Return self:found()

Method buscar() Class ${prefix}Collection
    self:lFound := self:getDao():buscar()
    self:goTop()
Return self:found()

Method bscChaPrim() Class ${prefix}Collection
    self:lFound := self:getDao():bscChaPrim()
    self:goTop()
Return self:found()

Method setEntity(oEntity) Class ${prefix}Collection
    self:oMapper:setEntity(oEntity)
    self:oDao:setHMap(oEntity:getHMap())
return

Method setRelation(oRelation) Class ${prefix}Collection
    self:hChildren:set(oRelation:getName(),oRelation)
Return

/*/
    Seta as chaves de busca dos filhos 
/*/
Method setKeyRelation() Class ${prefix}Collection

    Local nChild := 0
    Local nFrTo := 0
    Local aRelList := Nil
    Local aRelationKey := Nil

    self:hChildren:list(@aRelList)

    For nChild := 1 to Len(aRelList)
        
        oRelation := aRelList[nChild][COLLECTION]
        aRelationKey   := oRelation:getRelationKey()
        
        /*/
            Limpa vari�veis, em POST, faz o insert e depos a busca,
            variaveis ficam sujas para montar o Json de resposta
        /*/
        oRelation:getCollection():setHMap(THashMap():New())
        
        For nFrTo := 1 to len(aRelationKey)
            oRelation:getCollection():setValue(;
                aRelationKey[nFrTo][FIELD],self:getValue(aRelationKey[nFrTo][RELFIELD]);
            )
        Next
        self:setRelation(oRelation)
    Next

return

Method listRelations() Class ${prefix}Collection
    Local aRelList := {}
    self:hChildren:list(@aRelList)
return aRelList

Method isRelation(cName) Class ${prefix}Collection
Return self:hChildren:get(cName)

Method getRelation(cName) Class ${prefix}Collection
    Local oRelation := Nil
    self:hChildren:get(cName,@oRelation)
Return oRelation:getCollection()

Method applyExpand(aExpand) Class ${prefix}Collection
    
    Local lOk := .T.
    Local nExp := 1
    Local aExpands := {}
    Default aExpand := {}

    self:initRelation()
    
    For nExp := 1 to len(aExpand)
        aExpands := StrTokArr2(aExpand[nExp], ".")
        If self:isRelation(aExpands[NIVEL])
            If len(aExpands) > 1
                self:getRelation(aExpands[NIVEL]):initRelation()
                If !self:getRelation(aExpands[NIVEL]):isRelation(aExpands[SUBNIVEL]) 
                    //N�o � um relacionamento
                    lOk := .F.
                EndIf
            EndIf
        Else
            //N�o � um relacionamento
            lOk := .F.
        EndIf
    Next
    
return lOk
